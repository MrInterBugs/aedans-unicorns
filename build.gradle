plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
    id 'com.palantir.git-version' version "${git_version}"
}

def modVersion = gitVersion()
def loaderVersion = providers.gradleProperty("loader_version").get()
def fabricVersion = providers.gradleProperty("fabric_version").get()
def minecraftVersion = providers.gradleProperty("minecraft_version").get()
def reiVersion = providers.gradleProperty("rei_version").get()
def lombokVersion = providers.gradleProperty("lombok_version").get()
def archivesBaseName = providers.gradleProperty("archives_base_name").get()
def yarnMappings = providers.gradleProperty("yarn_mappings").get()

group = property('maven_group')
version = modVersion
base.archivesName = archivesBaseName

repositories {
    mavenCentral()
    maven { url = "https://maven.fabricmc.net/" }
    maven { url = "https://maven.shedaniel.me/" }
}

loom {
    splitEnvironmentSourceSets()
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings "net.fabricmc:yarn:${yarnMappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loaderVersion}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabricVersion}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-fabric:${reiVersion}"
    modLocalRuntime "me.shedaniel:RoughlyEnoughItems-fabric:${reiVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

processResources {
    inputs.property "version", modVersion
    filteringCharset = 'UTF-8'
    def expandProps = [
            version: modVersion,
            loader_version: loaderVersion,
            fabric_version: fabricVersion,
            minecraft_version: minecraftVersion
    ]
    filesMatching("fabric.mod.json") {
        expand(expandProps)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}
